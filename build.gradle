/*
This is the GRADLE build file for LIRE.
 */
plugins {
  id "de.thetaphi.forbiddenapis" version "3.1"
}
apply plugin: 'java'
apply plugin: 'project-report'

sourceCompatibility = 1.8
version = project.property('versionString') + '_build' + project.property('buildNumber')

def solrVersion = '7.6.0'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://maven.restlet.talend.com/'
    }
}

dependencies {
    // ---< Solr >---
    compile group: 'org.apache.solr', name: 'solr-core', version: solrVersion
    compile group: 'org.apache.solr', name: 'solr-dataimporthandler', version: solrVersion

    // ---< Commons >---
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

    // ---< Current version of LIRE >---
    compile fileTree(dir: 'lib', include: '*.jar')

    testCompile group: 'junit', name: 'junit', version: '4.12'
}


jar {
    Properties props = new Properties()
    File propsFile = new File("$project.rootDir/gradle.properties")
    props.load(propsFile.newDataInputStream())
    Integer nextbuildnum = (((props.getProperty('buildNumber')) as BigDecimal) + 1)
    props.setProperty('buildNumber', nextbuildnum.toString())

    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd-HHmm')
    props.setProperty('buildDate', formattedDate.toString())

    props.store(propsFile.newWriter(), null)

    archiveName = baseName+'-'+props.getProperty('versionString')+'.'+extension

    manifest {
        attributes 'Implementation-Title': 'LireSolr',
                'Implementation-Version': props.getProperty('versionString') + ' build' + nextbuildnum.toString() + ' date' + formattedDate.toString()
    }
}


/**
 * Deploys the results from the jar task to Solr.
 */
task distForSolr(type: Copy, dependsOn: jar) {
    delete './dist'
    String path = './dist'
    // change the path variable to copy it to your Solr installation directory ...
    // String path = '../../Applications/solr-7.5.0/server/solr-webapp/webapp/WEB-INF/lib'
    String lsJar = 'liresolr.jar'
    String liJar = 'lire.jar'
    delete path + lsJar
    delete path + liJar
    from('build/libs'){
        include 'LireSolr*.jar'
        rename {  lsJar }
    }
    from('lib'){
        include 'LIRE-*.jar'
        rename { liJar }
    }
    into path
}

task prepareForDockerImage(type: Copy, dependsOn: distForSolr) {
    String path = './liresolr-docker-build'
    delete path
    String lsJar = 'liresolr.jar'
    String liJar = 'lire.jar'
    from('build/libs'){
        include 'LireSolr*.jar'
        rename {  lsJar }
    }
    from('lib'){
        include 'LIRE-*.jar'
        rename { liJar }
    }
    into path
    from ('src/main/web') into path
    from ('src/main/docker') into path
}

task createFlickrDownloader(type: Jar, dependsOn: build) {
    archiveName = 'flickrdownloader.jar'
    destinationDir = project.getRootDir()

    from { Project.DEFAULT_BUILD_DIR_NAME + "/classes/main" }
    from { zipTree("lib/LIRE-1.0_b05.jar") }
    from { Project.DEFAULT_BUILD_DIR_NAME + "/resources/main" }

    manifest.attributes(
            'Main-Class': 'net.semanticmetadata.lire.solr.tools.FlickrSolrIndexingTool',
            'Class-Path': 'lire.jar'
    )

}

task runParallelIndexer(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.semanticmetadata.lire.solr.indexing.ParallelSolrIndexer'

    // arguments to pass to the application
    args('-i images-wipo.lst -f -o images-wipo.xml -n 4'.split(' '))
    /*
     use the XML file like this:
        curl http://localhost:8983/solr/lire/update  -H "Content-Type: text/xml" --data-binary @file.xml
     and maybe then
        curl http://localhost:8983/solr/lire/update?commit=true
      */
    //

}

task runImportFromCSV(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.semanticmetadata.lire.solr.indexing.ImportFromCSV'

    // run this with 
    // $> gradle runImportFromCSV --args='-i in.csv -o out.xml'
}

task runFlickrDownloader(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.semanticmetadata.lire.solr.tools.FlickrSolrIndexingTool'

    // arguments to pass to the application
    args('-o auto -n 20'.split(' '))
}

forbiddenApis {
  // TODO: bundledSignatures = [ 'jdk-unsafe', 'jdk-deprecated', 'jdk-non-portable', 'jdk-reflection' ]
  bundledSignatures = [ 'jdk-deprecated', 'jdk-non-portable', 'jdk-reflection' ]
}
